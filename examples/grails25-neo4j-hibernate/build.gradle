buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:2.2.0.RC1"
    }
}

apply plugin: "grails"

grails {
    grailsVersion = "2.5.5"
    groovyVersion = "2.4.6"
}

configurations.all { Configuration c ->
    c.exclude(group:'org.grails',module:'grails-datastore-gorm-support')
    c.exclude(group:'org.grails',module:'grails-datastore-simple')

    resolutionStrategy {
        eachDependency { DependencyResolveDetails details ->
            if(details.requested.group == 'org.grails' && !details.requested.name.startsWith('grails-datastore')) {
                details.useVersion(grailsVersion)
            }
        }
    }
    resolutionStrategy.dependencySubstitution {
        // Substitute project and module dependencies
        substitute project(':grails-datastore-gorm-support') with project(':grails-datastore-gorm-grails2-support')
    }
}

repositories {
    maven { url "https://repo.grails.org/grails/plugins" }
    maven { url "https://repo.grails.org/grails/core" }
}


dependencies {
    compile "org.grails.plugins:hibernate4:$hibernatePluginVersion",{
        exclude module: 'xml-apis'
    }
    compile "org.grails.plugins:neo4j:$rootProject.version"
    compile "javax.servlet:javax.servlet-api:3.1.0"

    test "org.spockframework:spock-core:1.0-groovy-2.4"
    test "org.neo4j.test:neo4j-harness:$neo4jVersion"
}

//test.jvmOptions.jvmArgs = ['-Xdebug','-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=5005']